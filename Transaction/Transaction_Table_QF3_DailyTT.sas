*Error Checking;
%include "E:\Shared\CADA\SAS Source Code\Development\nrochester\TRANSACTION_DATAMART_DAILY\Transaction_Table_Error_Check.sas";

*Libname Statements;
%include "E:\Shared\CADA\SAS Source Code\Development\nrochester\Libname_Statements.sas";
%INCLUDE "E:\SHARED\CADA\SAS SOURCE CODE\DEVELOPMENT\JHUBBARD\TOP SECRET PROGRAM.SAS";

*CUSTOMER JOIN PROGRAM;
%INCLUDE "E:\SHARED\CADA\SAS Source Code\Development\nrochester\TRANSACTION_DATAMART_DAILY\CUSTOMER_SSN_MACRO.SAS";
%SSN(QFUND3TT);

/* MACROS FOR SELECTING DATA */
%PUT &SYSUSERID;
%LET START=INTNX('DTDAY',DHMS(18628,0,0,0),1,'BEGINNING');
%LET THIRTYDAYS=INTNX('DTDAY',DHMS(%SYSFUNC(TODAY()),0,0,0),-5,'BEGINNING');
%PUT &START;
%PUT &THIRTYDAYS;

DATA _NULL_;
	CALL SYMPUTX('TRAN_LOGPATH',"E:\SHARED\CADA\LOGS\SKYNET V2",'G');
	CALL SYMPUTX('TRAN_FILE_PATH',"E:\SHARED\CADA\SAS SOURCE CODE\PRODUCTION\SKYNET V2\SKYNET REDESIGN\DATAMART REDESIGN\TRANSACTION",'G');
RUN;
/* START TIMER */
%LET _TIMER_START = %SYSFUNC(DATETIME());

/* DOWNLAOD LOAN TRANSACTION INFORMATION */
DATA WORK.TRANS_SUMMARY_QF3;
	SET EDW.TITLE_TRANS_SUMMARY;
WHERE ETL_CREATE_DATE_TIME > &THIRTYDAYS. AND PRODUCT_TYPE="TLP";
%RUNQUIT(&job,&sub18);

/* DOWNLOAD LOAN INFORMATION */
DATA WORK.TITLE_LOAN_SUMMARY;
	SET EDW.TITLE_LOAN_SUMMARY;
%RUNQUIT(&job,&sub18);

/* SORT LOAN TRANSACTION INFORMATION */
PROC SORT DATA=WORK.TRANS_SUMMARY_QF3;
BY LOAN_CODE LOAN_TRAN_CODE CUSTNBR TRAN_ID VOID_ID LOCNBR TRANDTE ETL_CREATE_DATE_TIME LOAN_STATUS PRODUCT_TYPE;
%RUNQUIT(&job,&sub18);

/* TRANSPOSE APPLIED CODE COLUMNS DOWN FROM LOAN TRANSACTION */
PROC TRANSPOSE DATA=WORK.TRANS_SUMMARY_QF3 OUT=WORK.LOAN_TRANSPOSE_QF3_TT;
VAR PRINCIPAL CSO_FEE LATE_FEE WRITEOFF_AMT WRITEOFF_FEE;
BY LOAN_CODE LOAN_TRAN_CODE CUSTNBR TRAN_ID VOID_ID LOCNBR TRANDTE ETL_CREATE_DATE_TIME LOAN_STATUS PRODUCT_TYPE;
%RUNQUIT(&job,&sub18);

/* ONLY TAKE TRANSACTIONS THAT ARE NOT ZERO FOR JOINING */
DATA WORK.LOAN_TRANSPOSE_NOZEROQF3_TT (RENAME=(COL1=TRANAMT _LABEL_=POSAPPLIEDCD));
SET WORK.LOAN_TRANSPOSE_QF3_TT;
WHERE COL1 NOT IN (0,.);
%RUNQUIT(&job,&sub18);

/* JOIN BASE POS INFORMATION TOGETHER */
PROC SQL;
	CREATE TABLE TRANSACTION_TABLE_QF3_T AS	
		SELECT 
			A.LOCNBR AS LOCNBR,
			A.LOAN_CODE AS DEALNBR1,
			A.LOAN_TRAN_CODE AS DEALTRANNBR1,
			A.VOID_ID AS VOIDFLG,
			A.TRAN_ID AS POSTRANCD,
			A.TRANDTE AS TRANDT,
			A.POSAPPLIEDCD,
			A.TRANAMT,
			A.ETL_CREATE_DATE_TIME AS TRANCREATEDT FORMAT=DATETIME20.,
			C.LOAN_DATE AS DEAL_DT FORMAT=DATETIME20.,
			A.PRODUCT_TYPE AS PRODUCTCD,
			A.CUSTNBR AS CUSTNBR1,
			B.SSN AS SSN,
			A.LOAN_STATUS AS DEALSTATUSCD LENGTH=15
		FROM WORK.LOAN_TRANSPOSE_NOZEROQF3_TT A
			LEFT JOIN (&SQL_TEXT.) B ON (B.CUSTNBR=A.CUSTNBR)
			LEFT JOIN WORK.TITLE_LOAN_SUMMARY C ON (C.LOAN_CODE=A.LOAN_CODE)
		WHERE A.ETL_CREATE_DATE_TIME > &THIRTYDAYS.;
%RUNQUIT(&job,&sub18);

/* CONVERT NUMERIC FIELDS TO CHARACTER */
DATA TRANSACTION_TABLE_QF3_TT (DROP=DEALNBR1 DEALTRANNBR1 /*ORIGTRANNBR1*/ CUSTNBR1);
	SET TRANSACTION_TABLE_QF3_T;
     IF PUT(CUSTNBR1,15.)~='.'            THEN CUSTNBR=PUT(STRIP(CUSTNBR1),15.);
ELSE IF PUT(STRIP(CUSTNBR1),15.)='.'      THEN CUSTNBR='';
     IF PUT(STRIP(DEALNBR1),15.)~='.'     THEN DEALNBR=PUT(STRIP(DEALNBR1),15.);
ELSE IF PUT(STRIP(DEALNBR1),15.)='.'      THEN DEALNBR='';
	 IF PUT(STRIP(DEALTRANNBR1),15.)~='.' THEN DEALTRANNBR=PUT(STRIP(DEALTRANNBR1),15.);
ELSE IF PUT(DEALTRANNBR1,15.)='.'         THEN DEALTRANNBR='';
%RUNQUIT(&job,&sub18);

/* CREATE 5 YEAR PORTION OF POS INFORMATION */
PROC SQL;
	CREATE TABLE TRANSACTION_TABLE_QF3_UPDATETT AS
		SELECT DISTINCT
			(CASE WHEN T1.PRODUCTCD IN ("TLP","TTOC") THEN "TITLE" 
				  WHEN T1.PRODUCTCD IN ("ILP", "FAI") THEN "INSTALLMENT" 
/*				  WHEN T1.PRODUCTCD = "TTOC" THEN "TEXAS TITLE"*/
				  ELSE "UNKNOWN" END) AS 
            	  PRODUCT LENGTH=20 FORMAT=$20.,
			(CASE WHEN T1.PRODUCTCD = "TLP"  THEN "TX TITLE" 
                  WHEN T1.PRODUCTCD = "ILP"  THEN "TX TETL"
				  WHEN T1.PRODUCTCD = "TTOC" THEN "TX TTOC"
				  WHEN T1.PRODUCTCD = "FAI"  THEN "TX FAI"
                  ELSE "UNKNOWN" END) 
				  AS PRODUCTDESC  LENGTH=30 FORMAT=$30.,
			'QFUND' AS POS LENGTH=20  FORMAT=$20.,
			'QFUND3' AS INSTANCE LENGTH=20  FORMAT=$20.,
			'STOREFRONT'		AS CHANNELCD,
			T2.ST_PVC_CD AS STATE LENGTH=2 FORMAT=$2.,
			T1.LOCNBR,
			T1.SSN,
			T1.CUSTNBR,
			''		AS OMNINBR,
			DHMS(DATEPART(T1.DEAL_DT),0,0,0) AS DEAL_DT 		FORMAT=DATETIME20.,
			T1.DEAL_DT AS DEAL_DTTM								FORMAT=DATETIME20.,
			T1.DEALNBR,
			LEFT("0") AS TITLE_DEALNBR LENGTH=15 FORMAT=$15.,
			T1.DEALTRANNBR,
			"" AS ORIGTRANNBR,
			T1.VOIDFLG LENGTH=2 FORMAT=$2.,
			. AS VOIDDT FORMAT=DATETIME20.,
			T1.DEALSTATUSCD LENGTH=15,
			T1.POSTRANCD LENGTH=50 FORMAT=$50.,
   COALESCE(T8.STNDTRANCD,'UNKNOWN') AS STNDTRANCD LENGTH=25 FORMAT=$25.,
			T1.POSAPPLIEDCD LENGTH=50 FORMAT=$50.,
 (CASE WHEN T1.POSTRANCD='PAY' AND T1.POSAPPLIEDCD='IFC' THEN 'INTEREST FEE CHARGED' ELSE
   COALESCE(T7.STNDAPPLIEDCD,'UNKNOWN')END) AS STNDAPPLIEDCD LENGTH=20 FORMAT=$20.,
   			T8.CI_FLG,
   			"" AS MONETARYCD LENGTH=15,
			T1.TRANAMT,
			T1.TRANDT,
			T1.TRANCREATEDT,
			DHMS(DATEPART(T1.TRANCREATEDT),0,0,0) AS BUSINESSDT FORMAT=DATETIME20.,
			DHMS(DATEPART(T1.TRANDT),0,0,0) 	  AS TRANDATE   FORMAT=DATETIME20.,
			%SYSFUNC(DATETIME()) AS UPDATEDT FORMAT=DATETIME20.,
			"" AS NCP_IND LENGTH=1,
			. AS CREATEUSR
		FROM TRANSACTION_TABLE_QF3_TT T1
			LEFT JOIN EDW.D_LOCATION T2 ON T2.LOC_NBR=T1.LOCNBR
			LEFT JOIN BIOR.L_APPLIEDCODES T7 ON T7.POSAPPLIEDCD=T1.POSAPPLIEDCD
			LEFT JOIN BIOR.L_TRANSACTIONCODES T8 ON (T8.POS='QFUND3' AND T8.POSTRANCD=T1.POSTRANCD)
		WHERE T1.TRANCREATEDT > &THIRTYDAYS.;
%RUNQUIT(&job,&sub18);

/* SORT POS INFORMATION */
/* STORE IN SAS DATA FOLDER */
PROC SORT DATA=WORK.TRANSACTION_TABLE_QF3_UPDATETT OUT=TRANSACTION_TABLE_QF3_UPDATETT NODUPKEY DUPOUT=DUPSDELETEQF3;
BY DEALNBR DEALTRANNBR POSTRANCD POSAPPLIEDCD TRANAMT TITLE_DEALNBR;
%RUNQUIT(&job,&sub18);

PROC FORMAT;
    PICTURE CHECKTHEDAY OTHER=%0Y.%0M.%0D (DATATYPE=DATE);
    PICTURE CHECKTHETIME OTHER=%0H.%0M.%0S (DATATYPE=TIME);
RUN;

%LET DATE=%SYSFUNC(INTNX(DAY,%SYSFUNC(TODAY()),0,END),DATE7.);
%PUT &DATE;

/* CREATE MACROS FOR BULKLOAD PATH AND TIMESTAMP */
DATA _NULL_;
	CALL SYMPUTX('TIMESTAMP',TRANWRD(PUT(DATETIME(),DATETIME20.),':','.'),'G');
    CALL SYMPUTX('PATH',"E:\SHARED\CADA\SAS DATA\DATAMART\SKYNET REDESIGN BULKLOAD LOGS\TRANSACTION\QFUND3\QFUND3TT",'G');
    CALL SYMPUTX('PATHTWO',"E:\SHARED\CADA\SAS DATA\DATAMART\SKYNET REDESIGN BULKLOAD LOGS\TRANSACTION\QFUND3\QFUND3TT\DIR2\",'G');
RUN;

PROC SQL;
    INSERT INTO SKY.TRAN_DATAMART_QF3TT (BULKLOAD=YES BL_LOG="&PATH.\BL_&DATE..LOG" BL_DELETE_DATAFILE=YES 
                                                   BL_DEFAULT_DIR="&PATHTWO.")
    SELECT 
        *
    FROM TRANSACTION_TABLE_QF3_UPDATETT;
QUIT;

/* STOP TIMER */
DATA _NULL_;
  DUR = DATETIME() - &_TIMER_START;
  PUT 30*'-' / ' TOTAL DURATION:' DUR TIME13.2 / 30*'-';
RUN;


/*UPLOAD QF3TT*/
%INCLUDE "&TRAN_FILE_PATH.\TRANSACTION_UPLOAD_QF3TT.SAS";
