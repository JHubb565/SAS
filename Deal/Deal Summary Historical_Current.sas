/*KEEP TRACK OF PARTITIONS*/

/*THIS PROGRAM SHOULD RUN ONCE MONTH AND CHECK TO SEE IF ITS BEEN ONE MONTH SINCE CREATING A PARTITION*/

%INCLUDE "\\CSSSASAPP\CADA\SAS SOURCE CODE\PRODUCTION\SERVICE ACCOUNTS\SVC_SASUSER.SAS";
%INCLUDE "E:\SHARED\CADA\SAS SOURCE CODE\PRODUCTION\STDM\STDM_LIBRARY_SCRIPT.SAS";
%INCLUDE "E:\SHARED\CADA\SAS SOURCE CODE\DEVELOPMENT\JHUBBARD\TOP SECRET PROGRAM.SAS";

%LET RUN=1;

OPTIONS SYMBOLGEN MPRINT MLOGIC;

%GLOBAL DATE;
%LET DATE=%SYSFUNC(INTNX(DAY,%SYSFUNC(TODAY()),0,END),DATE7.);
%PUT &DATE;

%LET DAY = %SYSFUNC(WEEKDAY("&DATE"D));
%PUT &DAY;

/*SEE IF ITS A SUNDAY AND IF IT'S BEEN A MONTH. IF ITS A SUNDAY AND IT HAS BEEN A MONTH WE WILL MOVE DATA FROM DEAL TO HISTORIC DEAL*/

%MACRO HIST_DECISION(RUN=,DAY=);
	
	%IF %EVAL(&RUN. = 1 AND &DAY. = 1) %THEN 
		%DO;
			PROC FORMAT;
			    PICTURE CHECKTHEDAY OTHER=%0Y.%0M.%0D (DATATYPE=DATE);
			    PICTURE CHECKTHETIME OTHER=%0H.%0M.%0S (DATATYPE=TIME);
			RUN;

			%LET DATE=%SYSFUNC(INTNX(DAY,%SYSFUNC(TODAY()),0,END),DATE7.);
			%PUT &DATE;

			/* CREATE MACROS FOR BULKLOAD PATH AND TIMESTAMP */
			DATA _NULL_;
				CALL SYMPUTX('TIMESTAMP',TRANWRD(PUT(DATETIME(),DATETIME20.),':','.'),'G');
			    CALL SYMPUTX('PATH',"E:\SHARED\CADA\SAS DATA\DATAMART\SKYNET REDESIGN BULKLOAD LOGS\TRANSACTION\TRANSACTION_HIST",'G');
			    CALL SYMPUTX('PATHTWO',"E:\SHARED\CADA\SAS DATA\DATAMART\SKYNET REDESIGN BULKLOAD LOGS\TRANSACTION\TRANSACTION_HIST\DIR2\",'G');
			RUN;

			/*DOWNLOAD THE CURRENT TRANSACTION TABLE FOR ANYTHING PRIOR TO 60 DAYS AND INSERT INTO THE HISTORICAL*/

			PROC SQL;
				CONNECT TO ORACLE(USER=&USER. PASSWORD=&PASSWORD. PATH='BIOR');
				CREATE TABLE ODSA_CURRENT AS
				SELECT *
				FROM CONNECTION TO ORACLE
				(
					SELECT *
					FROM BIOR.O_DEAL_SUMMARY_90
					WHERE DEAL_DT < TRUNC(CURRENT_DATE)-60
				)
				;
				DISCONNECT FROM ORACLE;	
			QUIT;


			PROC SQL;
			    INSERT INTO BIOR.O_DEAL_SUMMARY_HIS (BULKLOAD=YES BL_LOG="&PATH.\BL_&DATE..LOG" BL_DELETE_DATAFILE=YES 
			                                                   BL_DEFAULT_DIR="&PATHTWO.")
			    SELECT 
			        *
			    FROM ODSA_CURRENT;
			QUIT;


			PROC SQL;
				CONNECT TO ORACLE (USER=&USER. PASSWORD=&PASSWORD. PATH='BIOR');
				EXECUTE (DELETE FROM BIOR.O_DEAL_SUMMARY_90
						 WHERE DEAL_DT < TRUNC(CURRENT_DATE)-60) BY ORACLE;
				DISCONNECT FROM ORACLE;
			QUIT;

			OPTIONS EMAILSYS=SMTP EMAILHOST=MAIL.ADVANCEAMERICA.NET EMAILPORT=25;
			FILENAME MAIL EMAIL "NULL";
			DATA _NULL_;
				FILE MAIL
				TO=('BI_DATA@ADVANCEAMERICA.NET')
				CC=('SHOPKINS@ADVANCEAMERICA.NET')
				SUBJECT="DEAL HISTORICAL TRANSFER COMPLETE";
			RUN;

		%END;

%MEND;

%HIST_DECISION(RUN=&RUN.,DAY=&DAY.)

