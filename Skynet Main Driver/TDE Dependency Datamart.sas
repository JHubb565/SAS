PROC IMPORT DATAFILE="E:\SHARED\CADA\SAS SOURCE CODE\DEVELOPMENT\JHUBBARD\SKYNET REDESIGN\MASTER_TDE_DEPENDENCY_LIST.XLSX"
	REPLACE
	DBMS=XLSX
	OUT=TDE_DEPENDENCY_DM;
RUN;

%LET FIRST_LEVEL = ('BIOR.O_DAILY_SUMMARY_ALL' 'BIOR.TRANSPOSE_DAILY_SUMMARY' 'BIOR.O_DAILY_SUMMARY_DIV' 'BIOR.O_CUSTOMER_ALL');

%LET SECOND_LEVEL = ('BIOR.O_DEAL_SUMMARY_ALL' 'BIOR.O_DEALTRANSACTION_ALL');

DATA ADD_DEPENDENCY_PRIORITY;
	SET TDE_DEPENDENCY_DM;
	IF TABLE IN &FIRST_LEVEL. THEN PRIORITY_LEVEL = 1;
	ELSE IF TABLE IN &SECOND_LEVEL. THEN PRIORITY_LEVEL = 2;
	ELSE PRIORITY_LEVEL = 3;
/*	SPECIFIC CHANGES*/
	IF TABLE IN ('BIOR.CUST_CATEGORY_DETAIL' 'BIOR.CUST_CATEGORY_DAILY_COUNT') THEN TABLE = 'CUSTOMER LIFECYCLE';
	IF TABLE IN ('BIOR.O_DAILY_SUMMARY_DIV') THEN TABLE = 'BIOR.O_DAILY_SUMMARY_ALL';
RUN;

/*TAKE MAX PRIORITY LEVEL FOR EACH DASHBOARD*/

PROC SQL;
	CREATE TABLE MIN_DASHBOARDS AS
	SELECT DASHBOARD
		  ,MAX(PRIORITY_LEVEL)	AS MIN_PRIORITY_LEVEL
	FROM ADD_DEPENDENCY_PRIORITY
	GROUP BY DASHBOARD
;
QUIT;

PROC SQL;
	CREATE TABLE DASHBOARD_TABLE_COUNT AS
	SELECT DASHBOARD
		  ,COUNT(*)					AS TABLE_DEPENDENT_COUNT
	FROM ADD_DEPENDENCY_PRIORITY
	GROUP BY DASHBOARD
;
QUIT;

PROC SQL;
	CREATE TABLE TDE_DEPENDENCY_DM_PRE AS
	SELECT UPCASE(A.DASHBOARD)	AS DASHBOARD_NAME
		  ,A.TABLE				AS DEPENDENCIES
		  ,B.MIN_PRIORITY_LEVEL	AS DASHBOARD_PRIORITY_LEVEL
		  ,C.TABLE_DEPENDENT_COUNT
	FROM ADD_DEPENDENCY_PRIORITY A
	LEFT JOIN MIN_DASHBOARDS B
		ON (A.DASHBOARD = B.DASHBOARD)
	LEFT JOIN DASHBOARD_TABLE_COUNT C
		ON (A.DASHBOARD = C.DASHBOARD)
;
QUIT;

/*UPLOAD TO SKYNET SCHEMA*/

PROC SQL;
DROP TABLE SKY.TDE_DEPENDENCY_DATAMART
;
QUIT;

PROC SORT DATA=TDE_DEPENDENCY_DM_PRE OUT=TDE_DEPENDENCY_DM_PRE_SRT NODUPKEY;
BY DASHBOARD_NAME DEPENDENCIES;
RUN;

PROC SQL;
	CREATE TABLE SKY.TDE_DEPENDENCY_DATAMART AS
	SELECT A.*
		  ,DATETIME()	FORMAT=DATETIME20. AS UPDATE_DATE
	FROM TDE_DEPENDENCY_DM_PRE_SRT A
;
QUIT;