OPTIONS NOXWAIT;
%INCLUDE "E:\SHARED\CADA\SAS SOURCE CODE\PRODUCTION\SKYNET V3\SKYNET REDESIGN\TOP SECRET PROGRAM.SAS";


%MACRO CL_DEPENDENCIES();

	/*QUERY RESULTS FROM THE LOOP TO MAKE SURE EACH INSTANCE IS FINISHED*/

	%DO %UNTIL (%EVAL(&COUNT_FINISHED. >= 17));
			
				PROC SQL;
					CREATE TABLE TEMP AS
					SELECT DISTINCT SOURCE
								    ,EADV_STATUS
									,QFUND1_INSTALL_STATUS
									,QFUND1_TITLE_STATUS
									,QFUND2_INSTALL_STATUS
									,QFUND3_TTOC_STATUS
									,QFUND3_TXTITLE_STATUS
									,QFUND3_TETL_STATUS
									,QFUND3_FAI_STATUS
									,QFUND4_PAYDAY_STATUS
									,QFUND4_TITLE_STATUS
									,QFUND5_PAYDAY_STATUS
									,QFUND5_INSTALL_STATUS
									,QFUND5_TITLE_STATUS
									,NG_STATUS
									,ONLINE_STATUS
									,LOC_STATUS
									,FUSE_STATUS
					FROM BIOR.DATAMART_STATUS
					WHERE SOURCE = 'CUSTOMER LIFECYCLE'
				;
				QUIT;

			/*REVERSE THE TRANSPOSE*/
				DATA TEMP_STATUS;
					SET TEMP;
					ARRAY VARS EADV_STATUS QFUND1_INSTALL_STATUS QFUND1_TITLE_STATUS QFUND2_INSTALL_STATUS QFUND3_TTOC_STATUS QFUND3_TXTITLE_STATUS QFUND3_TETL_STATUS
							   QFUND3_FAI_STATUS QFUND4_PAYDAY_STATUS QFUND4_TITLE_STATUS QFUND5_PAYDAY_STATUS QFUND5_INSTALL_STATUS QFUND5_TITLE_STATUS NG_STATUS
								ONLINE_STATUS LOC_STATUS FUSE_STATUS; 	
					DO _T = 1 TO DIM(VARS);              	
					  IF NOT MISSING(VARS[_T]) THEN DO;  	
					    INSTANCE=VNAME(VARS[_T]);           
					    STATUS=VARS[_T];                   	
					    OUTPUT;                          	
					  END;
					END;
					DROP EADV_STATUS QFUND1_INSTALL_STATUS QFUND1_TITLE_STATUS QFUND2_INSTALL_STATUS QFUND3_TTOC_STATUS QFUND3_TXTITLE_STATUS QFUND3_FAI_STATUS QFUND3_TETL_STATUS
							   QFUND4_PAYDAY_STATUS QFUND4_TITLE_STATUS QFUND5_PAYDAY_STATUS QFUND5_INSTALL_STATUS QFUND5_TITLE_STATUS NG_STATUS
								ONLINE_STATUS LOC_STATUS FUSE_STATUS _T;  *DROP THE OLD VARS (COLS) AND THE DUMMY VARIABLE _T;
				RUN;

			/*LOOK AT TABLES WHICH HAVE FINISHED*/
				DATA COMPLETED_TABLES;
					SET TEMP_STATUS;
					WHERE STATUS = 'FINISHED';
				RUN;

				PROC SQL NOPRINT;
					SELECT COUNT(*) INTO: COUNT_FINISHED /*THIS NUMBER SHOULD BE EQUAL TO 16*/
					FROM COMPLETED_TABLES
					WHERE STATUS = 'FINISHED';
				QUIT;

				%IF %EVAL(&COUNT_FINISHED. < 17) %THEN 
					%DO;
						/*SLEEPS FOR 300 SECONDS (5 MINUTES) UNTIL IT FINDS 16 FINISHED TABLES, IT WILL LOOP FOREVER UNTIL THE 16 FINISHED TABLES*/
						DATA SLEEP;
							CALL SLEEP(300,1);
						RUN;

					%END;

	%END;

	/* CREATE TIMESTAMP */
	PROC FORMAT;
		PICTURE WHATDAYISIT OTHER=%0Y.%0M.%0D (DATATYPE=DATE);
		PICTURE WHATTIMEISIT OTHER=%0H.%0M.%0S (DATATYPE=TIME);
	RUN;

	DATA _NULL_;
		CALL SYMPUTX('TIMESTAMP',TRANWRD(PUT(DATETIME(),DATETIME20.),':','.'),'G');
		CALL SYMPUTX('MONDAY_OR_NOT',(WEEKDAY(DATE())),'G');
	RUN;


	PROC SQL;
		CONNECT TO ORACLE(USER=&USER. PASSWORD=&PASSWORD. PATH='BIOR');
		CREATE TABLE CL_CHECK AS
		SELECT *
		FROM CONNECTION TO ORACLE
		(
			SELECT BUSINESS_DATE	AS BUSINESS_DATE
				  ,SUM(NEW_CUST_CNT)	AS NEW_CUSTOMER_CNT
				  ,SUM(NEW_REPEAT_CUST_CNT)	AS NEW_REPEAT_CUST_CNT
				  ,SUM(REDEEM_CUST_CNT)		AS REDEEM_CUST_CNT
				  ,SUM(REACTIVE_CUST_CNT)	AS REACTIVE_CUST_CNT
				  ,(CASE WHEN BUSINESS_DATE ^= TRUNC(CURRENT_DATE-1) THEN 'ERROR' ELSE 'SUCCESS' END)  AS STATUS
			FROM BIOR.CUST_CATEGORY_DAILY_COUNT
			WHERE BUSINESS_DATE IN (SELECT MAX(BUSINESS_DATE)
                                         FROM BIOR.CUST_CATEGORY_DAILY_COUNT)
            GROUP BY BUSINESS_DATE
                    ,(CASE WHEN BUSINESS_DATE ^= TRUNC(CURRENT_DATE) THEN 'ERROR' ELSE 'SUCCESS' END)
		)
		;
		DISCONNECT FROM ORACLE;
	QUIT;

	%LET TDY = %SYSFUNC(TODAY(),WEEKDATE30.);

	/* 	EMAIL BI_DATA AND SPENCER THE RESULTS OF THE QUERIES  */
	OPTIONS EMAILSYS=SMTP EMAILHOST=MAIL.ADVANCEAMERICA.NET EMAILPORT=25;
	FILENAME EML EMAIL TO=('BI_DATA@ADVANCEAMERICA.NET','BI_REPORTING@ADVANCEAMERICA.NET'
				,'FINANCEDEPT@ADVANCEAMERICA.NET','FINANCEPRODUCT@ADVANCEAMERICA.NET') CC=('SHOPKINS@ADVANCEAMERICA.NET') SUBJECT="CUSTOMER LIFECYCLE CHECK &TDY" CT='TEXT/HTML';
	ODS LISTING CLOSE;
	ODS HTML BODY=EML;
	ODS SELECT SQL_RESULTS;
	ODS NOPROCTITLE;

	PROC SQL;
	TITLE "CUSTOMER LIFECYCLE CHECK";
		SELECT *
			FROM CL_CHECK 
			;
	QUIT;

	
	/*-----------------------------------------------------------TDE PART------------------------------------------------------------*/



	%IF %EVAL(&MONDAY_OR_NOT. ^= 2) %THEN 
		%DO;
		/*GET DASHBOARDS THAT ONLY DEPEEND ON CUSTOMER LIFECYCLE AND PASS THROUGH TO NEW VARIABLE*/

			PROC SQL;
				CREATE TABLE DASHBOARD_LIST AS
				SELECT DISTINCT DASHBOARD_NAME
					  ,DEPENDENCIES
				FROM SKY.TDE_DEPENDENCY_DATAMART
				WHERE DEPENDENCIES = 'CUSTOMER LIFECYCLE'
			;
			QUIT;

			%INCLUDE "&SKYNETREDESIGN.\TDE BAT UPLOAD.SAS";
		%END;

%MEND;

%CL_DEPENDENCIES

/* KICK OFF LIFECYCLE BACKUP */
%INCLUDE "&CUST_FILE_PATH.\CUST LIFE BACKUPS AFTER RUN.SAS";
