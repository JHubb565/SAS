/*	THIS PROGRAM'S GOAL IS TO CHECK FOR 2 THINGS*/
/*	1) IS O_DEALTRANSACTION_ALL FINISHED*/
/*	2) IF O_DEALTRANSACTION_ALL IS FINISHED THEN KICK OFF ONLY DEAL SUMMARY DEPENDENCIES*/
OPTIONS NOXWAIT;
%INCLUDE "E:\SHARED\CADA\SAS SOURCE CODE\PRODUCTION\SKYNET V3\SKYNET REDESIGN\TOP SECRET PROGRAM.SAS";


%MACRO DAILY_DEPENDENCIES();

	/*QUERY RESULTS FROM THE LOOP TO MAKE SURE EACH INSTANCE IS FINISHED*/

	%DO %UNTIL (%EVAL(&COUNT_FINISHED. >= 17));
			
				PROC SQL;
					CREATE TABLE TEMP AS
					SELECT DISTINCT SOURCE
								    ,EADV_STATUS
									,QFUND1_INSTALL_STATUS
									,QFUND1_TITLE_STATUS
									,QFUND2_INSTALL_STATUS
									,QFUND3_TTOC_STATUS
									,QFUND3_TXTITLE_STATUS
									,QFUND3_TETL_STATUS
									,QFUND3_FAI_STATUS
									,QFUND4_PAYDAY_STATUS
									,QFUND4_TITLE_STATUS
									,QFUND5_PAYDAY_STATUS
									,QFUND5_INSTALL_STATUS
									,QFUND5_TITLE_STATUS
									,NG_STATUS
									,ONLINE_STATUS
									,LOC_STATUS
									,FUSE_STATUS
					FROM BIOR.DATAMART_STATUS
					WHERE SOURCE = 'BIOR.O_DAILY_SUMMARY_ALL'
				;
				QUIT;

			/*REVERSE THE TRANSPOSE*/
				DATA TEMP_STATUS;
					SET TEMP;
					ARRAY VARS EADV_STATUS QFUND1_INSTALL_STATUS QFUND1_TITLE_STATUS QFUND2_INSTALL_STATUS QFUND3_TTOC_STATUS QFUND3_TXTITLE_STATUS QFUND3_TETL_STATUS
							   QFUND3_FAI_STATUS QFUND4_PAYDAY_STATUS QFUND4_TITLE_STATUS QFUND5_PAYDAY_STATUS QFUND5_INSTALL_STATUS QFUND5_TITLE_STATUS NG_STATUS
								ONLINE_STATUS LOC_STATUS FUSE_STATUS; 	
					DO _T = 1 TO DIM(VARS);              	
					  IF NOT MISSING(VARS[_T]) THEN DO;  	
					    INSTANCE=VNAME(VARS[_T]);           
					    STATUS=VARS[_T];                   	
					    OUTPUT;                          	
					  END;
					END;
					DROP EADV_STATUS QFUND1_INSTALL_STATUS QFUND1_TITLE_STATUS QFUND2_INSTALL_STATUS QFUND3_TTOC_STATUS QFUND3_TXTITLE_STATUS QFUND3_FAI_STATUS QFUND3_TETL_STATUS
							   QFUND4_PAYDAY_STATUS QFUND4_TITLE_STATUS QFUND5_PAYDAY_STATUS QFUND5_INSTALL_STATUS QFUND5_TITLE_STATUS NG_STATUS
								ONLINE_STATUS LOC_STATUS FUSE_STATUS _T;  *DROP THE OLD VARS (COLS) AND THE DUMMY VARIABLE _T;
				RUN;

			/*LOOK AT TABLES WHICH HAVE FINISHED*/
				DATA COMPLETED_TABLES;
					SET TEMP_STATUS;
					WHERE STATUS = 'FINISHED';
				RUN;

				PROC SQL NOPRINT;
					SELECT COUNT(*) INTO: COUNT_FINISHED /*THIS NUMBER SHOULD BE EQUAL TO 16*/
					FROM COMPLETED_TABLES
					WHERE STATUS = 'FINISHED';
				QUIT;

				%IF %EVAL(&COUNT_FINISHED. < 17) %THEN 
					%DO;
						/*SLEEPS FOR 300 SECONDS (5 MINUTES) UNTIL IT FINDS 16 FINISHED TABLES, IT WILL LOOP FOREVER UNTIL THE 16 FINISHED TABLES*/
						DATA SLEEP;
							CALL SLEEP(300,1);
						RUN;

					%END;

	%END;
	
	/*-----------------------------------------------------------TDE PART------------------------------------------------------------*/

%MEND;

%DAILY_DEPENDENCIES

	/* CREATE TIMESTAMP */
	PROC FORMAT;
		PICTURE WHATDAYISIT OTHER=%0Y.%0M.%0D (DATATYPE=DATE);
		PICTURE WHATTIMEISIT OTHER=%0H.%0M.%0S (DATATYPE=TIME);
	RUN;

	DATA _NULL_;
		CALL SYMPUTX('TIMESTAMP',TRANWRD(PUT(DATETIME(),DATETIME20.),':','.'),'G');
		CALL SYMPUTX('MONDAY_OR_NOT',(WEEKDAY(DATE())),'G');
	RUN;

	/*KICK OFF BUDGET ACTUALS*/
	SYSTASK COMMAND "'C:\PROGRAM FILES\SASHOME\SASFOUNDATION\9.4\SAS.EXE'
					 'E:\SHARED\CADA\SAS SOURCE CODE\PRODUCTION\REPORTING\BUDGET_ACTUALS_V2.SAS'
					 -LOG 'E:SHARED\CADA\LOGS\SKYNET_DAILY_SUMMARY_BUDGET_ACTUALS_V2_&TIMESTAMP..LOG'
					 -CONFIG 'C:\PROGRAM FILES\SASHOME\SASFOUNDATION\9.4\SASV9.CFG'"
					 TASKNAME=BUDGET
					 STATUS=BUDGET;

	/*KICK OFF O_SAP_ALL*/
	SYSTASK COMMAND "'C:\PROGRAM FILES\SASHOME\SASFOUNDATION\9.4\SAS.EXE'
					 'E:\SHARED\CADA\SAS SOURCE CODE\PRODUCTION\SAP REVENUE\SAP_REVENUE_ALL.SAS'
					 -LOG 'E:SHARED\CADA\LOGS\O_SAP_ALL_&TIMESTAMP..LOG'
					 -CONFIG 'C:\PROGRAM FILES\SASHOME\SASFOUNDATION\9.4\SASV9.CFG'"
					 TASKNAME=SAP
					 STATUS=SAP;


	/*KICK OFF DIV_DAILY_SUMMARY*/
	SYSTASK COMMAND "'C:\PROGRAM FILES\SASHOME\SASFOUNDATION\9.4\SAS.EXE'
					 '&DAILY_FILE_PATH.\STDM_STEP21_DIVSUMMARY_CURRENT.SAS'
					 -LOG '&DAILY_LOGPATH.\DIV_DAILY_&TIMESTAMP..LOG'
					 -CONFIG 'C:\PROGRAM FILES\SASHOME\SASFOUNDATION\9.4\SASV9.CFG'"
					 TASKNAME=DIV_DAILY
					 STATUS=DIV_DAILY;

	WAITFOR _ALL_ DIV_DAILY;

	/*	QUERY THAT CHECKS TO SEE IF DAILY SUMMARY HAS DATA FOR PREVIOUS RUN */
		PROC SQL;
		    CREATE TABLE DAILY_SUMMARY_CHECK AS
		        SELECT
		             INSTANCE
		            ,PRODUCT
		            ,DATEPART(MAX(BUSINESSDT)) AS BUSINESSDT FORMAT MMDDYY10.
		            ,CASE WHEN 
		                  WEEKDAY(TODAY()) = 2 AND CALCULATED BUSINESSDT >= TODAY()-2 OR INSTANCE = 'EAPROD1' AND PRODUCT = 'INSTALLMENT' THEN 'SUCCESSFUL'
		                  WHEN
		                  CALCULATED BUSINESSDT >= TODAY()-1 THEN 'SUCCESSFUL' 
		                  ELSE 'ERROR' 
		             END AS STATUS
		        FROM BIOR.O_DAILY_SUMMARY_ALL
		    WHERE BUSINESSDT >= DHMS(TODAY()-5,00,00,00)
		    GROUP BY INSTANCE
		             ,PRODUCT
		    ORDER BY INSTANCE
		             ,PRODUCT
		;
		QUIT;

		%LET TDY = %SYSFUNC(TODAY(),WEEKDATE30.);

		/* 	EMAIL BI_DATA AND SPENCER THE RESULTS OF THE QUERIES  */
		OPTIONS EMAILSYS=SMTP EMAILHOST=MAIL.ADVANCEAMERICA.NET EMAILPORT=25;
		FILENAME EML EMAIL TO=('BI_DATA@ADVANCEAMERICA.NET','BI_REPORTING@ADVANCEAMERICA.NET','FINANCEDEPT@ADVANCEAMERICA.NET'
							   ,'FINANCEPRODUCT@ADVANCEAMERICA.NET') CC=('SHOPKINS@ADVANCEAMERICA.NET') SUBJECT="DAILY SUMMARY CHECK &TDY" CT='TEXT/HTML';
		ODS LISTING CLOSE;
		ODS HTML BODY=EML;
		ODS SELECT SQL_RESULTS;
		ODS NOPROCTITLE;

		PROC SQL;
		TITLE "DAILY SUMMARY CHECK";
			SELECT A.*
				FROM DAILY_SUMMARY_CHECK A
				;
		QUIT;


%MACRO MONDAY_OR_NOT();
	%IF %EVAL(&MONDAY_OR_NOT. ^= 2) %THEN
		%DO;
			/*GET DASHBOARDS THAT ONLY DEPEEND ON TRANSPOSE AND PASS THROUGH TO NEW VARIABLE*/

			PROC SQL;
				CREATE TABLE DASHBOARD_LIST AS
				SELECT DISTINCT DASHBOARD_NAME
					  ,DEPENDENCIES
				FROM SKY.TDE_DEPENDENCY_DATAMART
				WHERE DEPENDENCIES = 'BIOR.O_DAILY_SUMMARY_ALL' AND TABLE_DEPENDENT_COUNT = 1
			;
			QUIT;

			%INCLUDE "&SKYNETREDESIGN.\TDE BAT UPLOAD.SAS";
		%END;
%MEND;

%MONDAY_OR_NOT


