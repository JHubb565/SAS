/*GOAL OF THIS PROGRAM IS TO SCAN THE BIOR.DATAMART_STATUS TABLE AND PROVIDE HOURLY UPDATES ON THE TABLES PROGRESSION TO FINISHING DATA LOADS
	AUTHOR: JUSTIN HUBBARD
*/

%INCLUDE "E:\SHARED\CADA\SAS SOURCE CODE\PRODUCTION\SKYNET V3\SKYNET REDESIGN\TOP SECRET PROGRAM.SAS";


/*ETL CHECK MACRO LOOPS*/
	%INCLUDE "&WHEREPRGRMSAT_REDESIGN.\ETL_CHECK_REDESIGN.SAS";
	%INCLUDE "&WHEREPRGRMSAT_REDESIGN.\SKYNET ETL CHECK LOOP.SAS";


%MACRO EMAIL_CAPTURE_DIFFERENCE();


	%DO %UNTIL (%EVAL(&FINISHED_LOADS. >= 102));

		/*GET STATUS OF CURRENT BIOR.DATAMART_STATUS AND RESTART THE LOOP*/
		PROC SQL;
			CREATE TABLE TEMP AS
			SELECT DISTINCT SOURCE
						    ,EADV_STATUS
							,QFUND1_INSTALL_STATUS
							,QFUND1_TITLE_STATUS
							,QFUND2_INSTALL_STATUS
							,QFUND3_TTOC_STATUS
							,QFUND3_TXTITLE_STATUS
							,QFUND3_TETL_STATUS
							,QFUND3_FAI_STATUS
							,QFUND4_PAYDAY_STATUS
							,QFUND4_TITLE_STATUS
							,QFUND5_PAYDAY_STATUS
							,QFUND5_INSTALL_STATUS
							,QFUND5_TITLE_STATUS
							,NG_STATUS
							,ONLINE_STATUS
							,LOC_STATUS
							,FUSE_STATUS
			FROM BIOR.DATAMART_STATUS
		;
		QUIT;

		DATA TEMP_STATUS;
			SET TEMP;
			ARRAY VARS EADV_STATUS QFUND1_INSTALL_STATUS QFUND1_TITLE_STATUS QFUND2_INSTALL_STATUS QFUND3_TTOC_STATUS QFUND3_TXTITLE_STATUS QFUND3_TETL_STATUS
					   QFUND3_FAI_STATUS QFUND4_PAYDAY_STATUS QFUND4_TITLE_STATUS QFUND5_PAYDAY_STATUS QFUND5_INSTALL_STATUS QFUND5_TITLE_STATUS NG_STATUS
						ONLINE_STATUS LOC_STATUS FUSE_STATUS; 	
			DO _T = 1 TO DIM(VARS);              	
			  IF NOT MISSING(VARS[_T]) THEN DO;  	
			    INSTANCE=VNAME(VARS[_T]);           
			    STATUS=VARS[_T];                   	
			    OUTPUT;                          	
			  END;
			END;
			DROP EADV_STATUS QFUND1_INSTALL_STATUS QFUND1_TITLE_STATUS QFUND2_INSTALL_STATUS QFUND3_TTOC_STATUS QFUND3_TXTITLE_STATUS QFUND3_FAI_STATUS QFUND3_TETL_STATUS
					   QFUND4_PAYDAY_STATUS QFUND4_TITLE_STATUS QFUND5_PAYDAY_STATUS QFUND5_INSTALL_STATUS QFUND5_TITLE_STATUS NG_STATUS
						ONLINE_STATUS LOC_STATUS FUSE_STATUS _T;  
		RUN;

		PROC SQL;
			CREATE TABLE FINISHED_COUNT AS
			SELECT COUNT(*)	AS COUNT
			FROM TEMP_STATUS
			WHERE STATUS = 'FINISHED'
		;
		QUIT;

		PROC SQL;
			CREATE TABLE SUMMARY_TABLE AS
			SELECT SOURCE
				  ,SUM(CASE WHEN STATUS = 'FINISHED' THEN 1 ELSE 0 END) AS COUNT_FINISHED
				  ,COUNT(*)									   		    AS TOTAL
				  ,ROUND(CALCULATED COUNT_FINISHED / CALCULATED TOTAL,.02) FORMAT=PERCENT8.			AS PERCENT_FINISHED
			FROM TEMP_STATUS
			GROUP BY SOURCE
		;
		QUIT;

		DATA _NULL_;
			SET FINISHED_COUNT;
			CALL SYMPUTX("FINISHED_LOADS",COUNT,'G');
		RUN;

		%PUT &FINISHED_LOADS;

		/*GET TIME*/
		DATA _NULL_;
			CALL SYMPUTX('CURRENT_TIME',PUT(DATETIME(),DATEAMPM20.),'G');
			CALL SYMPUTX('WHATDAYISIT',WEEKDAY(DATE()),'G');
		RUN;

		%PUT &CURRENT_TIME;

		%IF %EVAL(&FINISHED_LOADS. >= 102) %THEN 

			%DO;

				/*EMAIL TEAM*/
					OPTIONS EMAILSYS=SMTP EMAILHOST=MAIL.ADVANCEAMERICA.NET EMAILPORT=25;
					FILENAME MYMAIL EMAIL "SKYNET@COMPLETE"; 
					DATA _NULL_;
					   FILE MYMAIL
							TO=('BI_DATA@ADVANCEAMERICA.NET','BI_REPORTING@ADVANCEAMERICA.NET','SHOPKINS@ADVANCEAMERICA.NET')
						    FROM=("SKYNET@COMPLETE")
							SENDER=("SKYNET@COMPLETE")
					     SUBJECT="ALL DATA LOADS ARE FINISHED FOR THE DAY - &CURRENT_TIME ";
					RUN;
			%END;

		%ELSE %IF %EVAL(&FINISHED_LOADS. < 102) %THEN 

			%DO;

				/*EMAIL BI_DATA AND SPENCER THE RESULTS OF MOST RECENT BIOR.DATAMART_STATUS TABLE*/
					OPTIONS EMAILSYS=SMTP EMAILHOST=MAIL.ADVANCEAMERICA.NET EMAILPORT=25;
					FILENAME EML EMAIL "SKYNET@STILL_RUNNING"
						TO=('BI_DATA@ADVANCEAMERICA.NET','BI_REPORTING@ADVANCEAMERICA.NET','SHOPKINS@ADVANCEAMERICA.NET') 
						FROM=("SKYNET@STILL_RUNNING")
						SENDER=("SKYNET@STILL_RUNNING")
						SUBJECT="BIOR.DATAMART_STATUS FOR &CURRENT_TIME" CT='TEXT/HTML';
					ODS LISTING CLOSE;
					ODS HTML BODY=EML;
					ODS NOPROCTITLE;

					TITLE 'BIOR.DATAMART_STATUS SUMMARY';
					PROC PRINT DATA=SUMMARY_TABLE NOOBS;
					VAR SOURCE PERCENT_FINISHED;
					RUN;

					TITLE 'BIOR.DATAMART_STATUS NOT FINISHED PROGRAMS';
					PROC PRINT DATA=TEMP_STATUS NOOBS;
					WHERE STATUS ^= 'FINISHED';
					RUN;

					ODS HTML CLOSE;
					ODS LISTING;

			%END;

		%SKYNET_ETL_CHECK

		%IF %EVAL(&WHATDAYISIT. = 7) %THEN
			%DO;
				DATA INSTANCE_ETL_CHECK;
					SET INSTANCE_ETL_CHECK;
					IF INSTANCE = 'ONLINE' THEN LOADED=1;
				RUN;
			%END;

		PROC SQL;
			CREATE TABLE FINISHED_SUM_ETL AS
			SELECT SUM(LOADED)	AS SUM
			FROM INSTANCE_ETL_CHECK
		;
		QUIT;

		PROC SQL;
			CREATE TABLE FINISHED_COUNT_ETL AS
			SELECT COUNT(*)	AS COUNT
			FROM INSTANCE_ETL_CHECK
		;
		QUIT;

		DATA _NULL_;
			SET FINISHED_COUNT_ETL;
			CALL SYMPUTX("FINISHED_COUNT_ETL",COUNT,'G');
		RUN;

		DATA _NULL_;
			SET FINISHED_SUM_ETL;
			CALL SYMPUTX("FINISHED_SUM_ETL",SUM,'G');
		RUN;

		%PUT &FINISHED_COUNT_ETL;
		%PUT &FINISHED_SUM_ETL;

		/*GET TIME*/
		DATA _NULL_;
			CALL SYMPUTX('CURRENT_TIME',PUT(DATETIME(),DATEAMPM20.));
			CALL SYMPUTX('WHATDAYISIT',WEEKDAY(DATE()),'G');
		RUN;

		%PUT &CURRENT_TIME;
		%PUT &WHATDAYISIT;

		%IF %EVAL(&FINISHED_COUNT_ETL. ^= &FINISHED_SUM_ETL.) %THEN 

			%DO;
			
				/*EMAIL BI_DATA AND SPENCER THE RESULTS OF MOST RECENT INSTANCE_ETL_CHECK TABLE*/
					OPTIONS EMAILSYS=SMTP EMAILHOST=MAIL.ADVANCEAMERICA.NET EMAILPORT=25;
					FILENAME MAIL EMAIL "ETL@STILL_RUNNING" 
							TO=('BI_DATA@ADVANCEAMERICA.NET','BI_REPORTING@ADVANCEAMERICA.NET','SHOPKINS@ADVANCEAMERICA.NET')
							FROM=("ETL@STILL_RUNNING")
							SENDER=("ETL@STILL_RUNNING")
					SUBJECT="ETL TABLE CHECK &CURRENT_TIME" CONTENT_TYPE="TEXT/HTML";
					ODS LISTING CLOSE;
					ODS HTML BODY=MAIL;
					TITLE 'ETL CHECK TABLE';
					PROC PRINT DATA=INSTANCE_ETL_CHECK NOOBS;
					VAR INSTANCE LOADED;
					RUN;
					ODS HTML CLOSE;
					ODS LISTING;

			%END;

	/*SLEEPS FOR 1800 SECONDS (30 MINUTES) THE LOOP WILL START AGAIN AFTER THE 30 MINUTES IS OVER*/
	%PUT SLEEPING TIME;
	DATA SLEEP;
		CALL SLEEP(1800,1);
	RUN;

	%END;

%MEND;

%EMAIL_CAPTURE_DIFFERENCE	


/*ABORT PROGRAM*/
%MACRO STOPPROGRAM();

	%IF %EVAL(1=1) %THEN %DO;
		%abort cancel;
	%END;

%MEND;

%STOPPROGRAM			
			

			
			


