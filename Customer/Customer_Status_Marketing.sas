%LET DATE_PULL = DHMS(TODAY()-1,0,0,0);

/*	COUNT # DAYS BETWEEN &NEW_DT & 18 MONTHS AGO */
DATA _NULL_;
	CALL SYMPUTX('NEW_DT',DATEPART(&DATE_PULL));
RUN;
%LET NUM_DAYS_18 = %SYSFUNC(INTCK(DAY,%SYSFUNC(INTNX(MONTH,&NEW_DT,-18,SAME)),&NEW_DT));

PROC SQL;
	CREATE TABLE WORK.CS_SETUP AS
	SELECT SSN
			,TRANSACTION_DT AS LCITD
			,BALANCE_AMT AS BALANCE
		,INTCK('DAY',DATEPART(TRANSACTION_DT),&NEW_DT) AS DAYS_SINCE_LCITD FORMAT=5.
	FROM BIOR.WT_CUST_LAST_TRAN_ACTIVITY
	;
QUIT;

DATA WORK.CUSTOMER_STATUS (KEEP=SSN CUST_STATUS);
	SET WORK.CS_SETUP;
	FORMAT CUST_STATUS $15.;
	IF LCITD = . THEN CUST_STATUS = 'NOT_A_CUSTOMER';
		ELSE IF DAYS_SINCE_LCITD <= 60 THEN CUST_STATUS = 'ACTIVE';
		ELSE IF DAYS_SINCE_LCITD > 60 AND DAYS_SINCE_LCITD <= &NUM_DAYS_18 THEN CUST_STATUS = 'INACTIVE';
		ELSE IF DAYS_SINCE_LCITD > &NUM_DAYS_18 AND BALANCE = 0 THEN CUST_STATUS = 'DORMANT';
		ELSE IF DAYS_SINCE_LCITD > &NUM_DAYS_18 AND BALANCE NE 0 THEN CUST_STATUS = 'SUSPENDED';
RUN;

PROC SQL;
DROP TABLE BIOR.MKTG_CUSTSTATUSCRNT;
CREATE TABLE BIOR.MKTG_CUSTSTATUSCRNT AS
SELECT *
FROM WORK.CUSTOMER_STATUS;
QUIT;